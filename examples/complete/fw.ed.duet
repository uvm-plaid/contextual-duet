
let main = pλ m  : ℕ,
              n  : ℕ,
              ε  : ℝ⁺,
              δ  : ℝ⁺,
              k  : ℕ
              .
              xs : 𝕄 [L∞, L∞|m, n⋅𝔻 ] ,
              ys : 𝕄 [L∞, U|m, 1⋅𝔻 ] ,
              ε  : ℝ⁺[ε],
              δ  : ℝ⁺[δ],
              k  : ℕ[k]
              ⇒
  let d = cols xs in
  let m₀ = mcreate[ L∞ | ℕ[1] , d ] { i , j ⇒ 0.0 } in
  let indexes = mcreate[ L∞ | ℕ[1] , ℕ[1]⋅d ] { i , j ⇒
                 ⟨ j % dyn d, sign (real (dyn j - dyn d)) ⟩ } in

  aloop[ δ ] k on m₀ <xs,ys> { t , θ ⇒
    let μ = 1.0 / ((real t)+2.0) in
    let s = ℝ⁺[1.0] / real (rows xs) in
    p ← exponential[s, ε] indexes <xs,ys> { x ⇒
          let ⟨c, s⟩ = x in let g = ∇[ LR | θ ; xs , ys ] in g#[idx ℕ[0], c] } ;
    let g₀ = mcreate[ L∞ | ℕ[1] , d ] { i , j ⇒ 0.0 } in
    let gₚ = (let ⟨i, s⟩ = indexes#[idx ℕ[0], p] in g₀#[idx ℕ[0],i ↦ s ⋅ real 100]) in

    let θ'  = mmap θ  { x ⇒ (1.0-μ) ⋅ x } in
    let gₚ' = mmap gₚ { x ⇒ μ ⋅ (100.0 ⋅ x) } in
    return mmap θ', gₚ' { x, y ⇒ x + y }
  }
in main
